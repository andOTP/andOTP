apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "org.shadowice.flocke.andotp"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 25
        versionName "0.6.3"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix = ".dev"
            versionNameSuffix = "-dev"
            testCoverageEnabled = true
        }
    }
    lintOptions {
          abortOnError false
    }
    dataBinding {
        enabled = true
    }
    flavorDimensions 'market'
    productFlavors {
        fdroid {
            dimension = 'market'
        }
        play {
            dimension = 'market'
        }
    }
    sourceSets {
        main {
            res.srcDirs += 'src/main/res_supertinyicons'
        }
    }
}

dependencies {
    ext.supportLibVersion = "28.0.0"

    implementation "commons-codec:commons-codec:1.12"
    implementation "com.android.support:support-media-compat:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.github.aakira:expandable-layout:1.6.0"
    implementation "com.heinrichreimersoftware:material-intro:1.6.2"
    implementation "com.journeyapps:zxing-android-embedded:3.6.0"
    implementation "com.vanniktech:vntnumberpickerpreference:1.0.0"
    implementation "me.zhanghai.android.materialprogressbar:library:1.4.2"
    implementation "org.sufficientlysecure:openpgp-api:12.0"
    implementation "com.leinardi.android:speed-dial:2.0.1"
    implementation "com.mikepenz:aboutlibraries:6.1.1"
}

task updateIcons {
    description = "Updates the built in icons from SuperTinyIcons repo"
    doLast {
        def resourcesFolder = rootProject.projectDir.toString() + "/app/src/main/res_supertinyicons/drawable"

        delete resourcesFolder
        copy {
            from rootProject.projectDir.toString() + "/SuperTinyIcons/images/android-vector-drawable"
            into resourcesFolder
            include "*.xml"
            exclude "sentry.xml", "gojek.xml", "vivino.xml", "elementaryos.xml", "rust.xml",
                    "lastpass.xml", "calendar.xml", "sentry.xml", "redhat.xml", "open_benches.xml",
                    "opencores.xml" //These files are currently broken (need upstream fix)
            rename { it.replace("-", "_") }
        }

        def files = fileTree(resourcesFolder).filter { it.isFile() }.files.name.collect { it.replace(".xml", "") }
        def enumValues = files.collect { "${it.capitalize()}(R.drawable.${it})" }
        def enumValuesString = ""
        enumValues.each {
            enumValuesString += System.lineSeparator + "\t\t${it},"
        }

        copy {
            from rootProject.projectDir.toString() + "/app/src/main/java/org/shadowice/flocke/andotp/Utilities/EntryThumbnail.tmpl"
            into rootProject.projectDir.toString() + "/app/src/main/java/org/shadowice/flocke/andotp/Utilities"
            rename { it.replace(".tmpl", ".java") }
            filter { line -> line.replaceAll('_TEMPLATE_ENTRY_THUMBNAILS_', enumValuesString) }
        }


    }
}

preBuild.dependsOn updateIcons
